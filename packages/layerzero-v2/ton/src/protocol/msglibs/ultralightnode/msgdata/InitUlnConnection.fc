#include "../../../../funC++/classlib.fc";

#include "UlnSendConfig.fc";
#include "UlnReceiveConfig.fc";

;; required storage name
const int md::InitUlnConnection::NAME = "initUlnCon"u;

;; field names
const int md::InitUlnConnection::ulnSendConfigOApp = 0;
const int md::InitUlnConnection::ulnReceiveConfigOApp = 1;
const int md::InitUlnConnection::endpointAddress = 2;
const int md::InitUlnConnection::channelAddress = 3;

cell md::InitUlnConnection::New(
    cell $ulnSendConfigOApp,
    cell $ulnReceiveConfigOApp,
    int endpointAddress,
    int channelAddress
) impure inline method_id {
    return cl::declare(
        md::InitUlnConnection::NAME,
        unsafeTuple([
            [cl::t::objRef, $ulnSendConfigOApp],      ;; md::InitUlnConnection::ulnSendConfigOApp
            [cl::t::objRef, $ulnReceiveConfigOApp],   ;; md::InitUlnConnection::ulnReceiveConfigOApp
            [cl::t::address, endpointAddress],        ;; md::InitUlnConnection::endpointAddress
            [cl::t::address, channelAddress]          ;; md::InitUlnConnection::channelAddress
        ])
    );
}

cell md::InitUlnConnection::NewOnlyConfig(
    cell $ulnSendConfigOApp,
    cell $ulnReceiveConfigOApp
) impure inline method_id {
    return md::InitUlnConnection::New(
        $ulnSendConfigOApp,
        $ulnReceiveConfigOApp,
        NULLADDRESS,
        NULLADDRESS
    );
}

;; Ensure the Uln receive config does not contain garbage bits etc. that would cause
;; undefined behaviors in the protocol
cell md::InitUlnConnection::sanitize(cell $initUlnConnection) impure {
    cell $ulnSendConfigOApp = $initUlnConnection
        .cl::get<objRef>(md::InitUlnConnection::ulnSendConfigOApp);
    cell $ulnReceiveConfigOApp = $initUlnConnection
        .cl::get<objRef>(md::InitUlnConnection::ulnReceiveConfigOApp);
    int endpointAddress = $initUlnConnection
        .cl::get<address>(md::InitUlnConnection::endpointAddress);
    int channelAddress = $initUlnConnection
        .cl::get<address>(md::InitUlnConnection::channelAddress);

    return md::InitUlnConnection::New(
        UlnSendConfig::sanitize($ulnSendConfigOApp),
        UlnReceiveConfig::sanitize($ulnReceiveConfigOApp),
        endpointAddress,
        channelAddress
    );
}